apply plugin: 'maven'
apply plugin: 'signing'

def isReleaseBuild() {
    return VERSION_NAME.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return (project.hasProperty('releaseRepositoryUrl')
            ? project.releaseRepositoryUrl
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/")
}

def getSnapshotRepositoryUrl() {
    return (project.hasProperty('snapshotRepositoryUrl')
            ? project.snapshowRepositoryUrl
            : "https://oss.sonatype.org/content/repositories/snapshots/")
}

def getRepositoryUsername() {
    return project.hasProperty('nexusUsername') ? project.nexusUsername : ""
}

def getRepositoryPassword() {
    return project.hasProperty('nexusPassword') ? project.nexusPassword : ""
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME

                repository(url: getReleaseRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }
                snapshotRepository(url: getSnapshotRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = "src/main/java"
        android.libraryVariants.all { variant ->
            classpath += files("build/generated/source/aidl/${variant.name}")
            classpath += files("build/generated/source/r/${variant.name}")
            classpath += files("build/generated/source/buildConfig/${variant.name}")
            if (variant.name == 'release') {
                owner.classpath += variant.javaCompiler.classpath
            }
        }
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from "src/main/java"
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
